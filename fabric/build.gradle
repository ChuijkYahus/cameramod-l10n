plugins {
    id "com.github.johnrengelman.shadow" version "7.1.2"
}

architectury {
    platformSetupLoomIde()
    fabric()
}

loom {
    accessWidenerPath = project(":common").loom.accessWidenerPath
}

configurations {
    common
    shadowCommon // Don't use shadow from the shadow plugin because we don't want IDEA to index this.
    compileClasspath.extendsFrom common
    runtimeClasspath.extendsFrom common
    developmentFabric.extendsFrom common
}

processResources {
    filesMatching("fabric.mod.json") {
        expand rootProject.ext.replaceProperties + [project: project]
    }
}

shadowJar {
    exclude "architectury.common.json"

    configurations = [project.configurations.shadowCommon]
    archiveClassifier = "dev-shadow"
}

remapJar {
    exclude '**/example/**'

    injectAccessWidener = true
    input.set shadowJar.archiveFile
    dependsOn shadowJar
    archiveClassifier = null
    archiveClassifier = 'fabric'
}

jar {
    archiveClassifier = "dev"
}

sourcesJar {
    def commonSources = project(":common").sourcesJar
    dependsOn commonSources
    from commonSources.archiveFile.map { zipTree(it) }

    archiveClassifier = "fabric-sources"
}

remapSourcesJar{
    archiveClassifier = "fabric-sources"
}

components.java {
    withVariantsFromConfiguration(project.configurations.shadowRuntimeElements) {
        skip()
    }
}

publishing {
    publications {
        mavenFabric(MavenPublication) {
            artifactId = rootProject.mod_id + "-" + project.name
            from components.java
        }
    }
}
dependencies {
    modImplementation "net.fabricmc:fabric-loader:${rootProject.fabric_loader_version}"
    modApi "net.fabricmc.fabric-api:fabric-api:${rootProject.fabric_api_version}"


    common(project(path: ":common", configuration: "namedElements")) { transitive false }
    shadowCommon(project(path: ":common", configuration: "transformProductionFabric")) { transitive false }

    include(implementation(annotationProcessor("io.github.llamalad7:mixinextras-fabric:0.5.0")))

    modApi("net.mehvahdjukaar:moonlight-fabric:${moonlight_version}")


    modCompileOnly("curse.maven:sodium-394468:6382649")
    //modImplementation("curse.maven:irisshaders-455508:6213635")
    modImplementation("curse.maven:distant-horizons-508933:7336780")
    modCompileOnly("curse.maven:cc-tweaked-282001:5714511")
    //modImplementation("cc.tweaked-cobalt:cobalt:0.93")
   // modCompileOnly "com.terraformersmc:modmenu:4.0.6",
}
